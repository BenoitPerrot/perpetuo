description = "Perpetuo"

apply plugin: 'com.criteo.moab-module'
apply plugin: 'scala'
apply plugin: 'com.criteo.uberjar'
apply from: parentPomsFile('java-parent/parent.gradle')
apply from: parentPomsFile('java-parent/scalatest.gradle')

ext {
    artifactId = "perpetuo-app"
    groupId = "com.criteo.perpetuo"
    h2db_version = "1.4.197"
    main_class = "com.criteo.perpetuo.app.main"
    minimizeJar = "false"
    perpetuo_version = project.version
    scala_version = scala211_version
    scala_version_short = "2.11"
    slick_version = "3.2.3"
    twitter_version = "17.12.0"
    webapp_directory = "src/main/webapp"
    finagle_version = twitter_version
    finatra_version = twitter_version
    twitter_server_version = twitter_version
    twitter_util_version = twitter_version
    webapp_build_directory = "${webapp_directory}/build/default"
    log4j_scala_version = "11.0"
}

libraries {
    library "com.fasterxml.jackson.core:jackson-annotations:2.8.0" //com.fasterxml.jackson.core:jackson-annotations:2.8.4
    library "com.google.code.findbugs:jsr305:2.0.1" //com.google.code.findbugs:jsr305:3.0.1
    library "com.google.inject.extensions:guice-multibindings:4.0" //com.google.inject.extensions:guice-multibindings:4.1.0
    library "com.google.inject:guice:4.0" //com.google.inject:guice:4.1.0
    library "io.spray:spray-json_2.11:1.3.0" //io.spray:spray-json_2.11:1.3.1
    library "org.scalatest:scalatest_2.11:3.0.0" //org.scalatest:scalatest_2.11:2.2.4
}

sourceSets.main.resources {
    srcDir "src/main/webapp/build/default"
}

processResources {
    from("src/main/resources/version") {
        expand(['version': project.perpetuo_version])
    }
}

dependencies {
    compile "org.apache.httpcomponents:httpmime:4.5.3"
    compile "org.scala-lang:scala-library:${scala_version}"
    compile libraries["com.typesafe:config"]
    compile "com.typesafe.slick:slick_${scala_version_short}:${slick_version}"
    compile "com.h2database:h2:${h2db_version}"
    compile "com.zaxxer:HikariCP:2.7.6"
    compile "com.twitter:util-app_${scala_version_short}:${twitter_util_version}"
    compile "com.twitter:util-stats_${scala_version_short}:${twitter_util_version}"
    compile "com.twitter:util-core_${scala_version_short}:${twitter_util_version}"
    compile "com.twitter:finagle-http_${scala_version_short}:${finagle_version}"
    compile "com.twitter:finagle-core_${scala_version_short}:${finagle_version}"
    compile "com.twitter:finagle-stats_${scala_version_short}:${finagle_version}"
    compile "com.twitter:twitter-server_${scala_version_short}:${twitter_server_version}"
    compile "com.twitter:finatra-http_${scala_version_short}:${finatra_version}"
    compile "com.twitter:inject-utils_${scala_version_short}:${twitter_util_version}"
    compile "com.twitter:inject-app_${scala_version_short}:${twitter_version}"
    compile "com.twitter:inject-server_${scala_version_short}:${twitter_server_version}"
    compile "com.twitter:inject-core_${scala_version_short}:${twitter_version}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j2_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: log4j2_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: log4j2_version
    compile group: 'org.apache.logging.log4j', name: "log4j-api-scala_${scala_version_short}", version: log4j2_version
    compile "com.google.inject:guice:4.0"
    compile "io.spray:spray-json_${scala_version_short}:1.3.0"
    compile "org.codehaus.groovy:groovy-all:2.4.10"
    compile "org.codehaus.groovy:groovy-jsr223:2.4.6"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2"
    compile "com.samstarling:finagle-prometheus_${scala_version_short}:0.0.11"
    compile group: 'io.prometheus', name: 'simpleclient_hotspot', version: prometheus_simpleclient_version
    testCompile "org.scalatest:scalatest_${scala_version_short}:3.0.0"
    testCompile libraries["org.mockito:mockito-all"]
    testCompile group: "com.twitter", name: "finatra-http_${scala_version_short}", version: "${finatra_version}", classifier: "tests"
    testCompile libraries["junit:junit"]
    testCompile group: "com.twitter", name: "inject-app_${scala_version_short}", version: "${twitter_version}", classifier: "tests"
    testCompile group: "com.twitter", name: "inject-core_${scala_version_short}", version: "${twitter_version}", classifier: "tests"
    testCompile group: "com.twitter", name: "inject-modules_${scala_version_short}", version: "${twitter_version}", classifier: "tests"
    testCompile group: "com.twitter", name: "inject-server_${scala_version_short}", version: "${twitter_server_version}", classifier: "tests"
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
            "-unchecked",
            "-deprecation",
            "-feature",
            "-Xfatal-warnings",
            "-Ywarn-dead-code",
            "-Ywarn-numeric-widen"
    ]
}

task npm_installExec(type: CrossPlatformExec) {
    executable "npm"
    args "install"
    workingDir "${webapp_directory}"
}

task bower_installExec(type: CrossPlatformExec) {
    executable "bower"
    args "install"
    workingDir "${webapp_directory}"
}

task polymer_buildExec(type: CrossPlatformExec, dependsOn: ['npm_installExec', 'bower_installExec']) {
    executable "node"
    args "node_modules/polymer-cli/bin/polymer.js", "build"
    workingDir "${webapp_directory}"
}

tasks.processResources.dependsOn(polymer_buildExec)

test {
    // deactivate tests of the java plugin because they are launched by the scala plugin
    enabled = false
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

shadowJar {
    append("reference.conf")
    manifest {
        attributes("Main-Class": "${main_class}")
    }
}
